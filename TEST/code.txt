;====================================================================
; Main.asm file generated by New Project wizard
;
; Created:   Sat Apr 20 2024
; Processor: AT89C51
; Compiler:  ASEM-51 (Proteus)
;====================================================================

$NOMOD51
$INCLUDE (8051.MCU)

;====================================================================
; DEFINITIONS
;====================================================================

;====================================================================
; VARIABLES
;====================================================================
      TGXANH	EQU	30H
      TGVANG	EQU	31H
      TGDO	EQU	32H
      TIME	EQU	33H

;====================================================================
; RESET and INTERRUPT VECTORS
;====================================================================

      ; Reset Vector
      org   0000h
      jmp   Start
      
      ORG	000BH ; ngat timer 0
      LJMP T0_ISR

      org   0100h
Start:
      MOV TMOD, #11H
      MOV TH0, #0EEH
      MOV TL0, #06H
      MOV IE, #87H
      
      SETB TR0
      
      MOV TGXANH, #7
      MOV TGVANG, #3
      MOV TGDO, #10
      MOV TIME, #0
      
      MOV R0, #0 ; CHU KÌ
      MOV R1, TGXANH ; GIAY 7-3-10
      MOV R2, #1 ; 1:XANH, 2:VANG, 3:DO
      
      MOV R3, TGDO
      MOV R4, #1 ;1:DO, 2:XANH, 3:VANG
      
      MOV R5, #1; 0: CHAY BT, 1-2-3: CHINH DEN XANH-VANG-DO
      
      CLR P3.0
      CLR P3.7
Loop:	
      CALL KEYPAD
      jmp Loop
      
;====================================================================
;HAM DELAY      
DELAY:
    MOV R7, #255
      DL1:
	 DJNZ R7, DL1
RET
;====================================================================
;HAM HIENTHI
HIENTHI:
      ;quet led den 1
      CLR	P2.1
      MOV	A, R1
      MOV	B, #0AH
      DIV	AB
      MOV	P0, B
      CALL DELAY
      SETB	P2.1
      CLR	P2.0
      MOV	P0, A
      CALL DELAY
      SETB	P2.0
      
      ;quet led den 2
      CLR	P2.3
      MOV	A, R3
      MOV	B, #0AH
      DIV	AB
      MOV	P0, B
      CALL DELAY
      SETB	P2.3
      CLR	P2.2
      MOV	P0, A
      CALL DELAY
      SETB	P2.2
RET
;====================================================================
;HAM NGAT TIMER
T0_ISR:
      CPL	P2.7
      MOV TH0, #0EEH
      MOV TL0, #06H
      SETB TR0
      INC R0
      CJNE R0, #200, END_T0_ISR
      ;xu ly thoi gian den 1
      GIAY_1: 
	    DEC R1
	    MOV	R0, #0 
	    XANH:
	       CJNE R2, #1, VANG
	       CJNE R1, #0, CON_X
		  MOV R1, TGVANG
		  INC R2
		  JMP VANG
	       CON_X:
	       CLR P3.7
	       SETB P3.5
	    VANG:
	       CJNE R2, #2, DO
	       CJNE R1, #0, CON_V
		  MOV R1, TGDO
		  INC R2
		  JMP DO
	       CON_V:
	       CLR P3.6
	       SETB P3.7
	    DO:
	       CJNE R2, #3, GIAY_2
	       CJNE R1, #0, CON_DO
		  MOV R1, TGXANH
		  MOV R2, #1
		  SETB P3.5
		  JMP XANH
	       CON_DO:
	       CLR P3.5
	       SETB P3.6
      ;xu ly thoi gian den 2
      GIAY_2: 
	    DEC R3
	    DO_2:
	       CJNE R4, #1, XANH_2
	       CJNE R3, #0, CON_DO_2
		  MOV R3, TGXANH
		  INC R4
		  JMP XANH_2
	       CON_DO_2:
	       CLR P3.0
	       SETB P3.1
	    XANH_2:
	       CJNE R4, #2, VANG_2
	       CJNE R3, #0, CON_X_2
		  MOV R3, TGVANG
		  INC R4
		  JMP VANG_2
	       CON_X_2:
	       CLR P3.4
	       SETB P3.0
	    VANG_2:
	       CJNE R4, #3, END_T0_ISR
	       CJNE R3, #0, CON_V_2
		  MOV R3, TGDO
		  MOV R4, #1
		  SETB P3.1
		  JMP DO_2
	       CON_V_2:
	       CLR P3.1
	       SETB P3.4
      END_T0_ISR:
RETI
;====================================================================
;HAM KEYPAD
KEYPAD:
      MOV P1, #0F0H
      CALL HIENTHI
      MOV A, P1
      ANL A, #0F0H
      CJNE A, #0F0H, KEYPAD
CHONGDOI:
      LCALL DELAY
      CALL HIENTHI
      MOV A, P1
      ANL A, #0F0H
      CJNE A, #0F0H, KIEMTRACOT
      SJMP CHONGDOI
      KIEMTRACOT:;xu dung quet nut, 4 bit dau port1 dung de quet, 4 bit cuoi dung de nhan biet nut bam
	 MOV P1, #0F7H ; cho hang D = 0 1111 0111
	 MOV A, P1; lay tin hieu cua nut nhan
	 ANL A, #0F0H; and de lay nhung bit nut bam
	 CJNE A, #0F0H, SCAN_R0 ; kiem tra co su thay doi nut chua noi co nhay toi scan_r0
	 
	 MOV P1, #0FBH ; hang C 1111 1011
	 MOV A, P1
	 ANL A, #0F0H
	 CJNE A, #0F0H, SCAN_R1
	
	 MOV P1, #0FDH ; hang B 1111 1101
	 MOV A, P1
	 ANL A, #0F0H
	 CJNE A, #0F0H, SCAN_R2
	 
	 MOV P1, #0FEH ; hang A 1111 1110
	 MOV A, P1
	 ANL A, #0F0H
	 CJNE A, #0F0H, SCAN_R3
RET 

;====================================================================
;HAM NHAN PHIM
SCAN_R0: ;kiem tra cac cot o hang D - hang chuc nang
	 MOV DPTR, #MAHANGD ; gan thanh ghi dia chi mahang0
	 MOV B, #16 
	 DIV AB ; A luu 4 bit lon, B luu 4 bit nho
	 LCALL SCAN; neu khong phai c THI DUYET
	 RET
SCAN_R1:
	 MOV DPTR, #MAHANGC
	 MOV B, #16
	 DIV AB
	 LCALL SCAN
	 RET
SCAN_R2:
	 MOV DPTR, #MAHANGB
	 MOV B, #16
	 DIV AB
	 LCALL SCAN
	 RET
SCAN_R3:
	 MOV DPTR, #MAHANGA
	 MOV B, #16
	 DIV AB
	 LCALL SCAN
	 RET
SCAN:
	 RRC A ;quay phai a voi co nho c
	 JNC MATCH; neu co nho C = 0 thi nhay toi MATCH
	 INC DPTR; tang dia chi DPTR len 1 TUC LA SANG NUT KE TIEP
	 SJMP SCAN
	 MATCH:
	    MOV A, #0
	    MOVC A, @A + DPTR	;gan A = gtri cua mang tai dia chi DPTR
	    LCALL XULY
END_SCAN:
RET
;====================================================================
;HAM XULI NUT
XULY:
   CJNE A, #'C', CHECK
   CJNE R5, #0, C1
   INC R5
   LCALL RESET_ALL
   SETB TR0
   SJMP END_XULY
C1:
   CJNE R5, #1, C2
   MOV TIME, #0
   MOV R1, TIME
   MOV R3, TIME
   INC R5
   CLR TR0
   SJMP END_XULY
C2:
   CJNE R5, #2, C3
   MOV TIME, #0
   MOV R1, TIME
   MOV R3, TIME
   INC R5
   SJMP END_XULY
C3:
   CJNE R5, #3, END_XULY
   MOV TIME, #0
   MOV R1, TIME
   MOV R3, TIME
   MOV R5, #0
   SJMP END_XULY
CHECK:
   CJNE R5, #1, NHAPSO
   SJMP END_XULY
NHAPSO:
   MOV R6, A
   MOV A, TIME
   MOV B, #10
   MUL AB
   ADD A, R6
   MOV TIME, A
   
   MOV R1, TIME
   MOV R3, TIME
   
   CJNE R5, #2, NHAP_VANG
   MOV TGXANH, A
NHAP_VANG:
   CJNE R5, #3, NHAP_DO
   MOV TGVANG, A
NHAP_DO:
   CJNE R5, #0, END_XULY
   MOV TGDO, A
END_XULY:
RET
;====================================================================
;HAM SETTING TIME      
RESET_ALL:; ham setting Time
   SETB P3.5
   SETB P3.6
   
   SETB P3.1
   SETB P3.4
   
   CLR P3.0
   CLR P3.7
   
   MOV R0, #0 ; CHU KÌ
   MOV R1, TGXANH ; GIAY 7-3-10
   MOV R2, #1 ; 1: XANH, 2:VANG, 3:DO
      
   MOV R3, TGDO
   MOV R4, #1
RET      
;====================================================================
;DIA CHI CHUA MANG
ORG 300H; dia chi cac mang
MAHANGD: DB 'C', 0, '=', '0'
MAHANGC: DB 1, 2, 3, '0'
MAHANGB: DB 4, 5, 6, '0'
MAHANGA: DB 7, 8, 9, '0'

;====================================================================
      END